#include "razr_conj.grm"

%% == common rules for terminals ==

[NOUN] -> [TOKEN grm="С"];
[ADJ] -> [TOKEN grm="П"];
[SH-ADJ] -> [TOKEN grm="КР_ПРИЛ"];
[PART] -> [TOKEN grm="ПРИЧАСТИЕ"];
[SH-PART] -> [TOKEN grm="КР_ПРИЧАСТИЕ"];
[DEEPR] -> [TOKEN grm="ДЕЕПРИЧАСТИЕ"];
[V] -> [TOKEN grm="Г"];
[ADV] -> [TOKEN grm="Н"];
[INF] -> [TOKEN grm="ИНФИНИТИВ"];
[PRO] -> [TOKEN grm="МС"];
[PRO-ADJ] -> [TOKEN grm="МС-П"];
[PRED] -> [TOKEN grm="ПРЕДК"];
[DIG] -> [TOKEN type="DC"];
[PARTICLE] -> [TOKEN form="ЛИ" grm="ЧАСТ"] | [TOKEN form="ЖЕ" grm="ЧАСТ"] | [TOKEN form="БЫ" grm="ЧАСТ"] | [TOKEN form="УЖ" grm="ЧАСТ"] | [TOKEN form="ТОЛЬКО" grm="ЧАСТ"] | [TOKEN form="Ж" grm="ЧАСТ"] | [TOKEN form="Б" grm="ЧАСТ"] | [TOKEN form="ЛЬ" grm="ЧАСТ"];
[COMMA] -> ',';

%% == format_for_numbers ==

[NUMCARD] -> ([TOKEN grm="ЧИСЛ"] | [DIG]);
[NUMCARD] -> [NUMCARD] [NUMCARD root];

[NUMORD] -> ([TOKEN grm="ЧИСЛ-П"]);
[NUMORD] -> [NUMCARD] [NUMORD root];

[TYS] -> [TOKEN form="тысяча" grm="аббр"] '.';
[BIG-NUM] -> 'тысяча' | 'миллион' | 'миллиард' | [TYS];
[SMALL-DIG] -> '2' | '3' | '4' | '*2' | '*3' | '*4' ;				
				%%: $0.grm := set_all_cases($1.grm);
[SMALL-NUM] -> 'два' | 'три' | 'четыре' | [SMALL-DIG];

[NUMCARD] -> [SMALL-NUM] [BIG-NUM root grm="ед, рд"]
				: $0.grm := set_nomacc_plural($2.grm);
[NUMCARD] -> [NUMCARD] [BIG-NUM root grm="мн, рд"]
				: $0.grm := set_nomacc_plural($2.grm); 

[КОЛИЧ] -> [NUMCARD];
[КОЛИЧ] -> [NUMORD];

%% == format_for_num_complex ==

[СЛОЖ_ЧИСЛ] -> [DIG] [COMMA] [DIG]
				: $0.grm := set_all_cases($1.grm);

%% == format_for_similar_numerals ==
%% проблема с различением "23,7 и 5" и "23, 7 и 5" - как обратиться в правиле к наличию или отсутствию пробела между токенами?

[COORD] -> [COMMA] | 'и' | 'или';

%% нет сочинения между числительными и числами (цифрами)
[ОДНОР_ЧИСЛ1] -> [NUMCARD] [COORD] [NUMCARD root]
			: $0.grm := agrc($1.grm, $3.grm);
[ОДНОР_ЧИСЛ1] -> [ОДНОР_ЧИСЛ1] [COORD] [NUMCARD root]
			: $0.grm := agrc($1.grm, $3.grm);
[ОДНОР_ЧИСЛ2] -> [DIG] [COORD] [DIG];
[ОДНОР_ЧИСЛ2] -> [ОДНОР_ЧИСЛ2] [COORD] [DIG];
[ОДНОР_ЧИСЛ] -> [ОДНОР_ЧИСЛ1] | [ОДНОР_ЧИСЛ2];

%% == format_for_noun_num ==
%% как исключить из множества числительных для этого правила слова из списка num_pr.dat?

[СУЩ_ЧИСЛ] -> [TOKEN root filename="noun_num.dat"] ([ОДНОР_ЧИСЛ] | [КОЛИЧ] | [СЛОЖ_ЧИСЛ]);

%% == format_for_modif_adj_groups ==

[МОДИФ_ПРИЛ] -> ('такой' | 'самый') [ADJ root atomic]
		    : $0.grm := agrcng($1.grm, $2.grm);

%% == format_for_adv_adj ==

[ADV_ADJ] -> [TOKEN form="уж" grm="ЧАСТ"] [TOKEN filename="adv_adj.txt" root];
[ADV_ADJ] -> [TOKEN filename="adv_adj.txt" root] [TOKEN form="уж" grm="ЧАСТ"];
[ADV_ADJ] -> [TOKEN filename="adv_adj.txt"];

[НАР_ПРИЛ] -> [ADV_ADJ] ([ADJ root] | [SH-ADJ root] | [АНАТ_СРАВН root] | [PART root] | [SH-PART root] | [ADV root]);

%% == format_for_similar_adjs ==
%% , разгадавший загадки, новый - правило прич_сущ надо исправить, получается

[ADJ-COMP] -> [ADJ grm="сравн"];
[ADJP] -> [SH-ADJ] | [ADJ] | [НАР_ПРИЛ] | [PART] | [ПРЯМ_ДОП];

[ADJ-COMMA] -> [ADJP] [COMMA] [ADJP root]
		    : $0.grm := agrcng($1.grm, $3.grm);
[ADJ-COMMA] -> [ADJ-COMMA] [COMMA] [ADJP root]
		    : $0.grm := agrcng($1.grm, $3.grm);
[ОДНОР_ПРИЛ] -> [ADJ-COMMA];
[ОДНОР_ПРИЛ] -> ([ADJ-COMMA] | [ADJP]) ('и' | 'или') [ADJP root]
		    : $0.grm := agrcng($1.grm, $3.grm);

[ADJ-COMP-COMMA] -> [ADJ-COMP] [COMMA] [ADJ-COMP root];
[ADJ-COMP-COMMA] -> [ADJ-COMP-COMMA] [COMMA] [ADJ-COMP root];
[ОДНОР_ПРИЛ] -> [ADJ-COMP-COMMA];
[ОДНОР_ПРИЛ] -> ([ADJ-COMP-COMMA] | [ADJ-COMP]) ('и' | 'или') [ADJ-COMP root];

%% красной и синей ракетками

%% == format_for_similar_advs ==

[ADV-COMMA] -> [ADV] [COMMA] [ADV root];
[ADV-COMMA] -> [ADV-COMMA] [COMMA] [ADV root];
[ОДНОР_НАР] -> ([ADV-COMMA] | [ADV]) ('и' | 'или') [ADV root];

%% == format_for_similar_infs ==
%%	шить ботинки , пальто , печь хлеб , который посеял - дело в относительных предложениях

[ОДНОР_ИНФ] -> ([INF] | [ПРЯМ_ДОП grm="ИНФИНИТИВ"]) [COMMA] ([INF root] | [ПРЯМ_ДОП root grm="ИНФИНИТИВ"]);
[ОДНОР_ИНФ] -> [ОДНОР_ИНФ] [COMMA] ([INF root] | [ПРЯМ_ДОП root grm="ИНФИНИТИВ"]);
[ОДНОР_ИНФ] -> ([ОДНОР_ИНФ] | [INF root] | [ПРЯМ_ДОП root grm="ИНФИНИТИВ"]) ('и' | 'или') ([INF root] | [ПРЯМ_ДОП root grm="ИНФИНИТИВ"]);

%% == format_for_comp_adv ==

[СРАВН_СТЕПЕНЬ] -> [TOKEN filename="comp_adv.dat"] [ADV-ADJ-COMP root];
[ADV-ADJ-COMP] -> [ADV grm="сравн"] | [ОДНОР_НАР grm="сравн"] | [ADJ-COMP] | [ОДНОР_ПРИЛ grm="сравн"];

%% == format_for_adv_verb ==

[НАРЕЧ_ГЛАГОЛ] -> ([ADV] | [ОДНОР_НАР]) ([V root] | [INF root] | [PART root] | [SH-PART root]);
[НАРЕЧ_ГЛАГОЛ] -> [ADV] [ОДНОР_ИНФ root];

%% == format_for_selective_groups ==

%% аналог CanNumeralBeNoun
[SUBST-NUM] -> 'двое' | 'трое' | 'четверо' | 'пятеро' | 'шестеро' | 'семеро' | 'восьмеро' | 'девятеро' | 'десятеро' | 'оба';

[ELECT-1PART] -> 'какой' | 'один' | 'любой' | 'многие' | 'некоторые' | 'каждый' | [TOKEN form="многий" grm="мн"] | [TOKEN form="некоторый" grm="мн"] | [ADJ grm="прев"] | ('самый' [ADJ]) | [NUMORD] | [SUBST-NUM];

[ЭЛЕКТ_ИГ] -> [ELECT-1PART root] 'из' ([NOUN] | [PRO] | [ПРИЛ_СУЩ] | [PRO-ADJ]);

%% а это специальная составляющая для ПРИДАТ_ОПР - это старый комментарий
%% новый комментарий: а там надо ее где-то что ли?

[REL-ELECT] -> [ELECT-1PART root] 'из' 'который';
[ЭЛЕКТ_ИГ] -> [REL-ELECT];

%% == format_for_noun_groups ==

%% не красивую , но летнюю шапку - РАЗРЫВ_СОЮЗ
%% лежащем под ним ковре - НСО!
%% Готовый к новому большому и очень трудному походу молодой воин  - НСО!
%% Не красивая , а желтая страница - РАЗРЫВ_СОЮЗ
%% удобным по всем показателям новым методом - НСО!
%% И красивая , и желтая страница - РАЗРЫВ_СОЮЗ
%% ожидающему неприятностей мальчику - НСО!

[ПРИЛ_СУЩ] -> ([ADJ] | [МОДИФ_ПРИЛ] | [НАР_ПРИЛ] | [PRO-ADJ] | [NUMORD] | [АНАТ_СРАВН] | [ОДНОР_ПРИЛ grm="П"] | [TOKEN grm="ПРИЧАСТИЕ,дст"] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ,дст"]) ([NOUN root] | [ГЕНИТ_ИГ root] | [ЧИСЛ_СУЩ])
		    : $0.grm := agrcng($1.grm, $2.grm);
		    
[ПРИЛ_СУЩ] -> ([ADJ] | [МОДИФ_ПРИЛ] | [НАР_ПРИЛ] | [PRO-ADJ] | [NUMORD] | [АНАТ_СРАВН] | [ОДНОР_ПРИЛ grm="П"] | [TOKEN grm="ПРИЧАСТИЕ,дст"] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ,дст"]) [PARTICLE] ([NOUN root] | [ГЕНИТ_ИГ root] | [ЧИСЛ_СУЩ])
		    : $0.grm := agrcng($1.grm, $3.grm);
			
[ПРИЛ_СУЩ] -> ([ADJ] | [МОДИФ_ПРИЛ] | [НАР_ПРИЛ] | [PRO-ADJ] | [NUMORD] | [АНАТ_СРАВН]) [ПРИЛ_СУЩ root]
            : $0.grm := agrcng($1.grm, $2.grm);
            
[ПРИЛ_СУЩ] -> ([ADJ] | [МОДИФ_ПРИЛ] | [НАР_ПРИЛ] | [PRO-ADJ] | [NUMORD] | [АНАТ_СРАВН]) [PARTICLE] [ПРИЛ_СУЩ root]
            : $0.grm := agrcng($1.grm, $3.grm);
            
[ПРИЛ_СУЩ] -> [COMMA] ([ADJ] | [МОДИФ_ПРИЛ] | [НАР_ПРИЛ] | [PART] | [ОДНОР_ПРИЛ grm="П"] | [PRO-ADJ]) [COMMA] [PRO root]
            : $0.grm := agrcng($2.grm, $4.grm);

[ПРИЛ_СУЩ] -> [ОДНОР_ПРИЛ grm="П, ед"] [NOUN root grm="мн"]
            : $0.grm := agrc($1.grm, $2.grm);

[ПРИЛ_СУЩ] -> ([ADJ grm="мн"] | [МОДИФ_ПРИЛ grm="мн"] | [НАР_ПРИЛ grm="мн"]) [NOUN-COORD-SPECIAL root]
            : $0.grm := agrc($1.grm, $2.grm);

%% это такие специальные правила сочинения сущ, чтобы у первого конъюнкта было ед.ч., ну и без ветвления
[NOUN-COORD-SPECIAL] -> [NOUN grm="ед"] 'и' [NOUN root]
            : $0.grm := agrc($1.grm, $3.grm);
[NOUN-COORD-SPECIAL1] -> [NOUN] 'и' [NOUN root]
            : $0.grm := agrc($1.grm, $3.grm);
[NOUN-COORD-SPECIAL1] -> [NOUN] [COMMA] [NOUN-COORD-SPECIAL1 root]
            : $0.grm := agrc($1.grm, $3.grm);
[NOUN-COORD-SPECIAL] -> [NOUN grm="ед"] [COMMA] [NOUN-COORD-SPECIAL1 root]
            : $0.grm := agrc($1.grm, $3.grm);

%% == format_for_number_adverb ==

[НАР_ЧИСЛ_СУЩ] -> [TOKEN root filename="num_pr.dat"] ([ЧИСЛ_СУЩ grm="рд"] | [NOUN grm="рд, мн"] | [ПРИЛ_СУЩ grm="рд, мн"] | [ЭЛЕКТ_ИГ grm="рд, мн"]);

%% == format_for_number_noun ==

%% ЧТО-ТО Я ТУТ НАМУТИЛА НАДО РАЗОБРАТЬСЯ

%%	тысячи миль
%%	Одна тысяча пятьсот девяносто шесть участников - проблемы с тысячей, поправить правила числительных КОЛИЧ
%%	400 млрд долларов - бигнумы все еще и сущ, проблема
%%	двумя женскими головами , откинутыми назад - не приделывает "откинутыми назад"
%%	тридцать шестой человек - в ПРИЛ_СУЩ надо слева удалить NUMORD (хотя он там нужен) или в ГЕНИТ_ИГ, не знаю в общем в чем дело

[ЧИСЛ_СУЩ] -> ([КОЛИЧ grm="им"] | [КОЛИЧ grm="вн"] | [DIG] | [СЛОЖ_ЧИСЛ]) ([NOUN root grm="рд, мн"] | [ПРИЛ_СУЩ root grm="рд, мн"]);

[ЧИСЛ_СУЩ] -> [SMALL-NUM] ([NOUN root grm="мн"] | [ПРИЛ_СУЩ root grm="мн"])
            : $0.grm := agrc($1.grm, $2.grm);

[ЧИСЛ_СУЩ] -> ([SMALL-NUM grm="им"] | [SMALL-NUM grm="вн"] | [SMALL-DIG]) ([NOUN root grm="рд"] | [ПРИЛ_СУЩ root grm="рд"]);
            
[ЧИСЛ_СУЩ] -> ([КОЛИЧ] | [СЛОЖ_ЧИСЛ]) ([NOUN root grm="мн"] | [ПРИЛ_СУЩ root grm="мн"])
            : $0.grm := agrc($1.grm, $2.grm);

[ЧИСЛ_СУЩ] -> ([DIG] | [КОЛИЧ]) ([NOUN root] | [ПРИЛ_СУЩ root grm="мн"]);

%% == format_for_gen_chains ==

%%	Большинство мальчиков , игравших во дворе весь день - ПРИЧ_СУЩ - поставила
%%	Никакие вопросы поддержки работы с пользователем , документирования , тиражирования - ИГ "работа с пользователем"!
%%	ЛАЗАРЕВ В . П . Г  - слева поставить ФИО - поставила

[ГЕНИТ_ИГ] -> ([NOUN root] | [СУЩ_ЧИСЛ root] | [ЧИСЛ_СУЩ root] | [ГЕНИТ_ИГ root] | [ПРИЛ_СУЩ root] | [ФИО root]) ([NOUN grm="рд"] | [ПРИЛ_СУЩ grm="рд"] | [СУЩ_ЧИСЛ grm="рд"] | [ЧИСЛ_СУЩ grm="рд"] | [ГЕНИТ_ИГ grm="рд"] | [ОДНОР_ИГ grm="рд"] | [ПРИЧ_СУЩ grm="рд"]);

%% == format_for_from_compar ==

[ОТСРАВН] -> [ADJ root grm="сравн"] ([NOUN grm="рд"] | [PRO grm="рд"] | [ПРИЛ_СУЩ grm="рд"] | [СУЩ_ЧИСЛ grm="рд"] | [ЧИСЛ_СУЩ grm="рд"] | [ГЕНИТ_ИГ grm="рд"]);

%% == format_for_preps ==

%%	В коридоре , узком и темноватом - собрать нормально СУЩ_ОБС_ПРИЛ
%%	на пакеты , каждый из которых имел свой адрес назначения - относительное предложение
%%	с точки зрения автора - ?? "точки зрения автора" нету
%%	над двумя женскими головами , откинутыми назад - ПРИЧ_СУЩ "женскими головами , откинутыми назад"
%%	На лежащем под ним ковре - НСО!
%%	из бронированной машины Коммерческого банка , которая приписывается ее отцу . - относительное предложение
%%	Перед началом , в котором участвует специалист  - относительное предложение
%%	К ожидающему неприятностей мальчику - НСО!
%%	на столе , над которым висела лампа - относительное предложение
%%	в его сердце больном - ПРИЛ_ПОСТПОС - добавила
%%	до конца встречи , который принес - относительное предложение
%%	на столе , покрытом скатертью - в ПРИЧ_СУЩ добавить ИНСТР_ДОП
%%	от газов и пыли - разобраться почему
%%	от покупателей , благодаривших Гун за стиль - в ПРИЧ_СУЩ исправить

[PREP-NOM] -> [TOKEN filename="prep_nom.txt"];
[PREP-ACC] -> [TOKEN filename="prep_acc.txt"];
[PREP-GEN] -> [TOKEN filename="prep_gen.txt"];
[PREP-INSTR] -> [TOKEN filename="prep_instr.txt"];
[PREP-DAT] ->  [TOKEN filename="prep_dat.txt"];
[PREP-PREP] ->  [TOKEN filename="prep_prep.txt"];

%% +ПРИДАТ_ОПР
[PP-NOM] -> [PREP-NOM grm="ПРЕДЛ"] ([PRO-ADJ root grm="им"] | [NOUN root grm="им"] | [ЧИСЛ_СУЩ root grm="им"] | [ПРИЛ_СУЩ root grm="им"] | [PRO root grm="им"] | [СУЩ_ОБС_ПРИЛ root grm="им"] | [ГЕНИТ_ИГ root grm="им"] | [КОЛИЧ root grm="им"] | [СУЩ_ЧИСЛ root grm="им"] | [ADJ root grm="им"] | [ОДНОР_ПРИЛ root grm="им"] | [ПРИЧ_СУЩ root grm="им"] | [ПРИЛ_ПОСТПОС root grm="им"]);

[PP-ACC] -> [PREP-ACC grm="ПРЕДЛ"] ([PRO-ADJ root grm="вн"] | [NOUN root grm="вн"] | [ЧИСЛ_СУЩ root grm="вн"] | [ПРИЛ_СУЩ root grm="вн"] | [PRO root grm="вн"] | [СУЩ_ОБС_ПРИЛ root grm="вн"] | [ГЕНИТ_ИГ root grm="вн"] | [КОЛИЧ root grm="вн"] | [СУЩ_ЧИСЛ root grm="вн"] | [ADJ root grm="вн"] | [ОДНОР_ПРИЛ root grm="вн"] | [ПРИЧ_СУЩ root grm="вн"] | [ПРИЛ_ПОСТПОС root grm="вн"]);

[PP-GEN] -> [PREP-GEN grm="ПРЕДЛ"] ([PRO-ADJ root grm="рд"] | [NOUN root grm="рд"] | [ЧИСЛ_СУЩ root grm="рд"] | [ПРИЛ_СУЩ root grm="рд"] | [PRO root grm="рд"] | [СУЩ_ОБС_ПРИЛ root grm="рд"] | [ГЕНИТ_ИГ root grm="рд"] | [КОЛИЧ root grm="рд"] | [СУЩ_ЧИСЛ root grm="рд"] | [ADJ root grm="рд"] | [ОДНОР_ПРИЛ root grm="рд"] | [ПРИЧ_СУЩ root grm="рд"] | [ПРИЛ_ПОСТПОС root grm="рд"]);

[PP-INSTR] -> [PREP-INSTR grm="ПРЕДЛ"] ([PRO-ADJ root grm="тв"] | [NOUN root grm="тв"] | [ЧИСЛ_СУЩ root grm="тв"] | [ПРИЛ_СУЩ root grm="тв"] | [PRO root grm="тв"] | [СУЩ_ОБС_ПРИЛ root grm="тв"] | [ГЕНИТ_ИГ root grm="тв"] | [КОЛИЧ root grm="тв"] | [СУЩ_ЧИСЛ root grm="тв"] | [ADJ root grm="тв"] | [ОДНОР_ПРИЛ root grm="тв"] | [ПРИЧ_СУЩ root grm="тв"] | [ПРИЛ_ПОСТПОС root grm="тв"]);

[PP-DAT] -> [PREP-DAT grm="ПРЕДЛ"] ([PRO-ADJ root grm="дт"] | [NOUN root grm="дт"] | [ЧИСЛ_СУЩ root grm="дт"] | [ПРИЛ_СУЩ root grm="дт"] | [PRO root grm="дт"] | [СУЩ_ОБС_ПРИЛ root grm="дт"] | [ГЕНИТ_ИГ root grm="дт"] | [КОЛИЧ root grm="дт"] | [СУЩ_ЧИСЛ root grm="дт"] | [ADJ root grm="дт"] | [ОДНОР_ПРИЛ root grm="дт"] | [ПРИЧ_СУЩ root grm="дт"] | [ПРИЛ_ПОСТПОС root grm="дт"]);

[PP-PREP] -> [PREP-PREP grm="ПРЕДЛ"] ([PRO-ADJ root grm="пр"] | [NOUN root grm="пр"] | [ЧИСЛ_СУЩ root grm="пр"] | [ПРИЛ_СУЩ root grm="пр"] | [PRO root grm="пр"] | [СУЩ_ОБС_ПРИЛ root grm="пр"] | [ГЕНИТ_ИГ root grm="пр"] | [КОЛИЧ root grm="пр"] | [СУЩ_ЧИСЛ root grm="пр"] | [ADJ root grm="пр"] | [ОДНОР_ПРИЛ root grm="пр"] | [ПРИЧ_СУЩ root grm="пр"] | [ПРИЛ_ПОСТПОС root grm="пр"]);

[ПГ] -> [PP-NOM] | [PP-ACC] | [PP-GEN] | [PP-DAT] | [PP-INSTR] | [PP-PREP];

%% == format_for_similar_noun_groups ==

%%	хозяин , молодой парень , обесцвеченный городской - ну тут с последним коньюнктом проблема, разобраться
%%	жизнь скучную , одинокую - даже не знаю, зачем это надо
%%	ботинки , пальто , печь - тоже неправильно
%%	Богатство ассоциаций , блеск композиции , сила контрастов - что-то с тире
%%	поддержки работы с пользователем , документирования , тиражирования - опять работа с пользователем
%%	Этот хилый цыпленок , недоносок - тире

[ОДНОР_ИГ] -> ([NOUN] | [ПРИЛ_СУЩ] | [ГЕНИТ_ИГ] | [PRO]) ('и' | 'или') ([NOUN root] | [ПРИЛ_СУЩ root] | [ГЕНИТ_ИГ root] | [PRO root])
            : $0.grm := agrc($1.grm, $3.grm);
[ОДНОР_ИГ] -> [COORD-NP-COMMA] ('и' | 'или') ([NOUN root] | [ПРИЛ_СУЩ root] | [ГЕНИТ_ИГ root] | [PRO root])
            : $0.grm := agrc($1.grm, $3.grm);
[COORD-NP-COMMA] -> ([NOUN] | [ПРИЛ_СУЩ] | [ГЕНИТ_ИГ] | [PRO]) [COMMA] ([NOUN root] | [ПРИЛ_СУЩ root] | [ГЕНИТ_ИГ root] | [PRO root])
            : $0.grm := agrc($1.grm, $3.grm);
[COORD-NP-COMMA] -> [COORD-NP-COMMA] [COMMA] ([NOUN root] | [ПРИЛ_СУЩ root] | [ГЕНИТ_ИГ root] | [PRO root])
            : $0.grm := agrc($1.grm, $3.grm);
[ОДНОР_ИГ] -> [COORD-NP-COMMA];

%% == format_for_neg_verb ==

%%	не умирая - деепричастие
%% теперь надо поставить отр_форму в прям_доп

[ОТР_ФОРМА] -> [TOKEN form="не"] [TOKEN grm="Г" root];
%%([V root] | [INF root] | [DEEPR root]);

%% == format_for_dir_obj ==
%% 	производишь муру - не мура, а мур в дативе!
%%	увидел дом , который он , думая о детях , построил . - справа добавить ПРИДАТ_ОПР!
%%	разгадавший загадки , новый - а это зачем?
%% 	не дали Немцову - добавить ОТР_ФОРМУ - добавила
%%	более ощущал свое бессилие - НАРЕЧ_ГЛАГОЛ - добавила
%%	видел дом , ушедший под лед . - ПРИЧ_СУЩ - добавила
%%	всегда выводила ее - НАРЕЧ_ГЛАГОЛ - добавила
%%	не назвать её - добавить ОТР_ФОРМУ - добавила
%%	сейчас получит ответ - НАРЕЧ_ГЛАГОЛ - добавила
%%	злостно нарушал устав нашего общества - НАРЕЧ_ГЛАГОЛ - добавила
%%	шить ботинки , пальто , печь - а это зачем?
%%	Перехватившего пас соперника - побеждает другой вариант?
%%	производило впечатление необычное , страшное и очень неприятное - ПРИЛ_ПОСТПОС - добавила
%%	Перехватившего пас - побеждает другой вариант?
%%	печь хлеб , который посеял - относительное предложение
%%	не подняла его - добавить ОТР_ФОРМУ - добавила
%%	сначала заставила меня - НАРЕЧ_ГЛАГОЛ - добавила

[ПРЯМ_ДОП] -> ([TOKEN grm="дст, пе"] | [ОТР_ФОРМА grm="дст, пе"] | [НАРЕЧ_ГЛАГОЛ grm="дст, пе"]) ([NOUN grm="вн"] | [PRO grm="вн"] | [СУЩ_ЧИСЛ grm="вн"] | [ЭЛЕКТ_ИГ grm="вн"] | [ПРИЛ_СУЩ grm="вн"] | [НАР_ЧИСЛ_СУЩ grm="вн"] | [ЧИСЛ_СУЩ grm="вн"] | [ГЕНИТ_ИГ grm="вн"] | [ОДНОР_ИГ grm="вн"] | [РАЗРЫВ_СОЮЗ grm="С,вн"] | [ПРИЧ_СУЩ grm="вн"] | [ПРИЛ_ПОСТПОС grm="вн"]);

%% == format_for_verb_inf ==
%%	не удалось закончить образование - слева ОТР_ФОРМА - добавила, справа ПРЯМ_ДОП - добавила
%%	очень хотел научиться - слева НАРЕЧ_ГЛАГОЛ - добавила
%%	пытаясь решить задачу - слева DEEPR - добавила, справа  ПРЯМ_ДОП - добавила
%%	удобно думать - слева PRED - добавила
%%	очень хотел научиться если не писать , так читать - слева НАРЕЧ_ГЛАГОЛ - добавила, в качестве аналога тому как тут, справа просто приделать РАЗРЫВ_СОЮЗ - добавила
%% 	хочу и пить , и курить - РАЗРЫВ_СОЮЗ - добавила
%% 	не может интересовать - слева ОТР_ФОРМА - добавила
%%	Трудно жить - слева PRED - добавила
%%	не смогли решить задачу - слева ОТР_ФОРМА - добавила, справа ПРЯМ_ДОП - добавила
%%	не мог прийти - слева ОТР_ФОРМА - добавила
%%	хочу шить ботинки , пальто , печь - справа ПРЯМ_ДОП - добавила
%%	Завтра надо еще успеть уложить вещи - справа ПЕР_ГЛАГ_ИНФ, НАРЕЧ_ГЛАГОЛ - добавила, слева - НАР_ПРЕДИК - добавила
%%	досталось убирать квартиру - справа ПРЯМ_ДОП - добавила
%%	Завтра надо еще успеть
%%	осмелился примерить его цилиндр - справа ПРЯМ_ДОП - добавила
%%	приятно ощущать тепло - слева PRED - добавила
%%	хочу шить ботинки , пальто , печь хлеб , который посеял - относительное предложение, ПРЯМ_ДОП - добавила
%%	хотел оправдать свою фразу - справа ПРЯМ_ДОП - добавила
%%	приятно ощущать

%% добавить список глаголов, которые управляют инфинитивом
[ПЕР_ГЛАГ_ИНФ] -> ([V root] | [INF root] | [ОТР_ФОРМА root] | [НАРЕЧ_ГЛАГОЛ root] | [DEEPR root] | [PRED root] | [НАР_ПРЕДИК root]) ([INF] | [ОДНОР_ИНФ] | [ПРЯМ_ДОП grm="ИНФИНИТИВ"] | [РАЗРЫВ_СОЮЗ grm="ИНФИНИТИВ"] | [ПЕР_ГЛАГ_ИНФ grm="ИНФИНИТИВ"] | [НАРЕЧ_ГЛАГОЛ grm="ИНФИНИТИВ"]);


%% == format_for_noun_adj_postposition ==

[ПРИЛ_ПОСТПОС] -> ([NOUN root] | [ПРИЛ_СУЩ root]) ([ADJ] | [МОДИФ_ПРИЛ] | [ОДНОР_ПРИЛ grm="П"] | [НАР_ПРИЛ])
		    : $0.grm := agrcng($1.grm, $2.grm);

%% == format_for_noun_detached_adj_postposition ==

%%	загадки , новый - это зачем?
%%	коридоре , узком и темноватом - у меня туда включается запятая справа!
%%	этим вопросом , очень скучным и глупым - у меня туда включается запятая справа!
 
%% запятая или конец предложения справа. ни то ни другое в составляющую не включается. у меня пока запятая только, и та включается. как сделать?
[СУЩ_ОБС_ПРИЛ] -> ([NOUN root] | [ПРИЛ_СУЩ root]) [COMMA] ([ADJ] | [МОДИФ_ПРИЛ] | [ОДНОР_ПРИЛ grm="П"] | [НАР_ПРИЛ]) [COMMA]
		    : $0.grm := agrcng($1.grm, $3.grm);

[СУЩ_ОБС_ПРИЛ] -> [ОДНОР_ИГ root grm="ед"] [COMMA] ([ADJ grm="мн"] | [МОДИФ_ПРИЛ grm="мн"] | [ОДНОР_ПРИЛ grm="П, мн"] | [НАР_ПРИЛ grm="мн"]) [COMMA]
		    : $0.grm := agrc($1.grm, $3.grm);
[СУЩ_ОБС_ПРИЛ] -> ([NOUN root grm="мн"] | [PRO root grm="мн"] | [ПРИЛ_СУЩ root grm="мн"] | [ЧИСЛ_СУЩ root grm="мн"]) [COMMA] [ОДНОР_ПРИЛ grm="П, ед"] [COMMA]
		    : $0.grm := agrc($1.grm, $3.grm);

%% == format_for_partic_clause ==
%%	Человек , разгадавший загадки , новый
%%	Стена джунглей , тянувшаяся до горизонта и , наверно , еще на добрых две - ууу
%%	женскими головами , откинутыми назад - слева ПРИЛ_СУЩ - добавила
%%	покупателей , благодаривших Гун за стиль - добавить справа ПГ - добавила

[ПРИЧ_СУЩ] -> ([NOUN root] | [ПРИЛ_СУЩ root]) ([TOKEN grm="ПРИЧАСТИЕ"] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ"]);
[ПРИЧ_СУЩ] -> ([NOUN root] | [ПРИЛ_СУЩ root]) ([TOKEN grm="ПРИЧАСТИЕ"] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ"]) [ПГ];
[ПРИЧ_СУЩ] -> ([NOUN root] | [ПРИЛ_СУЩ root]) [COMMA] ([TOKEN grm="ПРИЧАСТИЕ"] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ"]);
[ПРИЧ_СУЩ] -> ([NOUN root] | [ПРИЛ_СУЩ root]) [COMMA] ([TOKEN grm="ПРИЧАСТИЕ"] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ"]) [ПГ];

%% == format_for_whose_clause ==

%% подлежащее с этими правилами не приделывается к глагольной группе почему-то, см "дом, который стоит на холме, упал"

%% подлежащная релятивизация, не хватает разных типов финитных клауз
%% [REL-CL] -> 'который' ([V] | [ПРЯМ_ДОП] | [НАРЕЧ_ГЛАГОЛ] | [ОТР_ФОРМА] | [ПЕР_ГЛАГ_ИНФ]);

%% неподлежащная релятивизация - фигово, и типов тоже не хватает
%% [REL-CL] -> 'который' [NOUN grm="им"] ([V] | [ПРЯМ_ДОП] | [НАРЕЧ_ГЛАГОЛ] | [ОТР_ФОРМА] | [ПЕР_ГЛАГ_ИНФ]);
%% [REL-CL] -> 'который' ([V] | [ПРЯМ_ДОП] | [НАРЕЧ_ГЛАГОЛ] | [ОТР_ФОРМА] | [ПЕР_ГЛАГ_ИНФ]) [NOUN grm="им"];
%% [REL-CL] -> [PREP] 'который' [NOUN grm="им"] ([V] | [ПРЯМ_ДОП] | [НАРЕЧ_ГЛАГОЛ] | [ОТР_ФОРМА] | [ПЕР_ГЛАГ_ИНФ]);
%% [REL-CL] -> [PREP] 'который' ([V] | [ПРЯМ_ДОП] | [НАРЕЧ_ГЛАГОЛ] | [ОТР_ФОРМА] | [ПЕР_ГЛАГ_ИНФ])  [NOUN grm="им"];

%% пакеты, каждый из которых имел свой адрес назначения
%% [REL-CL] -> [REL-ELECT] ([V] | [ПРЯМ_ДОП] | [НАРЕЧ_ГЛАГОЛ] | [ОТР_ФОРМА] | [ПЕР_ГЛАГ_ИНФ]);

%% [ПРИДАТ_ОПР] -> [NOUN root] [COMMA] [REL-CL] [COMMA];

%% == format_for_adv_predik ==
%% тут плохо дело! они не катят как PRED!

%% уж очень хорошо
%% уж очень же много
%% совсем скучно
%% завтра надо

[НАР_ПРЕДИК] -> [ADV] [PRED root];
[НАР_ПРЕДИК] -> [TOKEN form="уж" grm="ЧАСТ"] [ADV] [PRED root];
[НАР_ПРЕДИК] -> [TOKEN form="уж" grm="ЧАСТ"] [ADV] [TOKEN form="же" grm="ЧАСТ"] [PRED root];
 
%% == format_for_anat_compar ==

[АНАТ_СРАВН] -> ([TOKEN form="более" grm="Н"] | [TOKEN form="менее" grm="Н"]) ([ADJ root] | [SH-ADJ root]);

%% == format_for_noun_adjunct_group ==
%%	отца , очень обидчивого человека - если добавить справа запятую, собирается. в целиковом предложении нет.
%% 	мальчишка , бойкий , востроглазый чертенок - в целиковом предложении разбирается, так - нет из-за тире
%% 	Его отца , очень обидчивого человека - если добавить справа запятую, собирается. в целиковом предложении нет.
%%	хозяин , молодой парень - в целиковом предложении разбирается, так - нет из-за тире

%% согласование по числу и падежу, пока разбиваю на два правила
[ПРИЛОЖЕНИЕ] -> ([NOUN root grm="ед"] | [ПРИЛ_СУЩ root grm="ед"]) [COMMA] ([NOUN grm="ед"] | [ПРИЛ_СУЩ grm="ед"]) [COMMA]
		    : $0.grm := agrc($1.grm, $3.grm);

[ПРИЛОЖЕНИЕ] -> ([NOUN root grm="мн"] | [ПРИЛ_СУЩ root grm="мн"]) [COMMA] ([NOUN grm="мн"] | [ПРИЛ_СУЩ grm="мн"]) [COMMA]
		    : $0.grm := agrc($1.grm, $3.grm);
 
%% == format_for_FAM1_FAM2 ==

%% ЛАЗАРЕВ В.П. Г.САМАРА // ФИО не должны пересекаться

[ФИО] -> [TOKEN graf="FAM1"] [TOKEN graf="FAM2" root];
[ФИО] -> [TOKEN graf="FAM1"] [TOKEN atomic] [TOKEN graf="FAM2" root];
[ФИО] -> [TOKEN graf="FAM1"] [TOKEN atomic] [TOKEN atomic] [TOKEN graf="FAM2" root];
[ФИО] -> [TOKEN graf="FAM1"] [TOKEN atomic] [TOKEN atomic] [TOKEN atomic] [TOKEN graf="FAM2" root];

%% == format_for_web_addr,"ЭЛ_АДРЕС"  ==
[ЭЛ_АДРЕС] -> [TOKEN graf="EA"];

%% == format_for_rank_surname,"ДОЛЖ_ФИО"  ==
[ДОЛЖ_ФИО] -> [TOKEN root filename="profes.txt"] ([ФИО]|[TOKEN graf="NAM?"]);

%% == format_for_keyb,"КЛВ"  ==
[КЛВ] -> [TOKEN graf="KEY1"] [TOKEN atomic] [TOKEN graf="KEY2" root];


%% == format_for_oborots,"ОБОРОТ"  ==
%% [ОБОРОТ] -> [TOKEN graf="EXPR1"] [TOKEN graf="EXPR2" root];
%% [ОБОРОТ] -> [TOKEN graf="EXPR1"] [TOKEN atomic] [TOKEN graf="EXPR2" root];
%% [ОБОРОТ] -> [TOKEN graf="EXPR1"] [TOKEN atomic] [TOKEN atomic] [TOKEN graf="EXPR2" root];
%% [ОБОРОТ] -> [TOKEN graf="EXPR1"] [TOKEN atomic] [TOKEN atomic] [TOKEN atomic] [TOKEN graf="EXPR2" root];

%% == format_for_foreign_term,"ИНОСТР_ЛЕКС"  ==
[ИНОСТР_ЛЕКС] -> [TOKEN type="LLE"];

%% =====

[CLAUSE] -> 
[КОЛИЧ] | [СЛОЖ_ЧИСЛ] | [СУЩ_ЧИСЛ] | [МОДИФ_ПРИЛ] | 
[ОДНОР_ПРИЛ] | [ОДНОР_НАР] | [ОДНОР_ИНФ] | [СРАВН_СТЕПЕНЬ] | 
[НАРЕЧ_ГЛАГОЛ] | [ЭЛЕКТ_ИГ] | [ПРИЛ_СУЩ] | [НАР_ПРИЛ] | 
[НАР_ЧИСЛ_СУЩ] | [ЧИСЛ_СУЩ] | [ГЕНИТ_ИГ] | [ОТСРАВН] | 
[ПГ] | [ОДНОР_ИГ] | [ОТР_ФОРМА] | [ПРЯМ_ДОП] | 
[ПЕР_ГЛАГ_ИНФ] | [ПРИЛ_ПОСТПОС] | [СУЩ_ОБС_ПРИЛ] | [АНАТ_СРАВН] | [ПРИЛОЖЕНИЕ] |  [ОДНОР_ЧИСЛ] | 
[ФИО] | [ДОЛЖ_ФИО] | [ЭЛ_АДРЕС] | [КЛВ] |  [ИНОСТР_ЛЕКС] | [ПРИЧ_СУЩ] | [ПРЯМ_ДОП_РЕВ] | [РАЗРЫВ_СОЮЗ] | [НАР_ПРЕДИК] | [НАР_НАР];
%% [ОБОРОТ]
%% кандидаты на ИГ: NOUN; PRO; СУЩ_ЧИСЛ; ЭЛЕКТ_ИГ; ПРИЛ_СУЩ; НАР_ЧИСЛ_СУЩ; ЧИСЛ_СУЩ; ГЕНИТ_ИГ; ОДНОР_ИГ; ПРИЛ_ПОСТПОС; СУЩ_ОБС_ПРИЛ; ПРИЛОЖЕНИЕ; ДОЛЖ_ФИО; ФИО
%% кандидаты на ПрилГ: ADJ; МОДИФ_ПРИЛ; ОДНОР_ПРИЛ; ОДНОР_ПРИЛ1; ОДНОР_ПРИЛ2; ОДНОР_ПРИЛ3; СРАВН_СТЕПЕНЬ; НАР_ПРИЛ; АНАТ_СРАВН; PRO-ADJ
%% кандидаты на ГГ: V, INF, ОДНОР_ИНФ, НАРЕЧ_ГЛАГОЛ, ОТР_ФОРМА, ПРЯМ_ДОП; ПЕР_ГЛАГ_ИНФ

%%ПРАВИЛА КОТОРЫХ НЕТУ!

%% == format_for_dir_obj_rev ==
%%  его крали 
%% всех зазывал
%% их остановил
%% нас не замечают
%% его ударил

[ПРЯМ_ДОП_РЕВ] -> [PRO grm="вн"] ([TOKEN grm="дст, пе"] | [ОТР_ФОРМА grm="дст, пе"]);

%% == format_for_adv_adv ==
%%	очень непросто
%%	слишком же широко
%%	слишком уж широко
%%	уж очень хорошо
%%	совершенно непонятно
%%	совсем скучно
%%	так быстро

[НАР_НАР] -> [ADV] [ADV root];
[НАР_НАР] -> [TOKEN form="уж" grm="ЧАСТ"] [ADV] [ADV root];
[НАР_НАР] -> [ADV] [TOKEN form="уж" grm="ЧАСТ"] [ADV root];
[НАР_НАР] -> [ADV] [TOKEN form="же" grm="ЧАСТ"] [ADV root];
[НАР_НАР] -> [TOKEN form="уж" grm="ЧАСТ"] [ADV] [TOKEN form="же" grm="ЧАСТ"] [ADV root];

%% == format_for_adv_number_adverb ==

%%	очень уж много
%%	очень же много
%%	уж очень же много

[НАР_НАР_ЧИСЛ] -> [ADV] [TOKEN root filename="num_pr.dat"];
[НАР_НАР_ЧИСЛ] -> [TOKEN form="уж" grm="ЧАСТ"] [ADV] [TOKEN root filename="num_pr.dat"];
[НАР_НАР_ЧИСЛ] -> [ADV] [TOKEN form="же" grm="ЧАСТ"]  [TOKEN root filename="num_pr.dat"];
[НАР_НАР_ЧИСЛ] -> [ADV] [TOKEN form="уж" grm="ЧАСТ"]  [TOKEN root filename="num_pr.dat"];
[НАР_НАР_ЧИСЛ] -> [TOKEN form="уж" grm="ЧАСТ"] [ADV] [TOKEN form="же" grm="ЧАСТ"]  [TOKEN root filename="num_pr.dat"];

%% == format_for_approx_noun_prep ==

[АППРОКС_ПГ] -> [NOUN root] [ПГ grm="ЧИСЛ"];

%% == format_for_instr_obj ==

%%	не назвать её Симочкой
%%	раскидана ветром
%%	разогретого солнцем камня
%%	тепло разогретого солнцем камня
%%	работали старой пилой
%%	обесцвеченный городской одеждой
%%	шли полем
%%	прижатый токами воздуха
%%	подорванного его резкой девальвацией
%%	покрытом скатертью
%%	заполнены людьми
%%	пользовались удобным по всем показателям новым методом
%%	шли почтой

%% добавить управлеие из файла и исправить перходнсоть пассив
[ИНСТР_ДОП] -> ([V root grm="пе"] | [ПРЯМ_ДОП root grm="пе"] | [PART root grm="пе"] | [НАРЕЧ_ГЛАГОЛ root grm="пе"]) ([NOUN grm="тв"] | [ГЕНИТ_ИГ grm="тв"] | [ПРИЛ_СУЩ grm="тв"]);

[ИНСТР_ДОП] -> ([V root grm="стр"] | [ПРЯМ_ДОП root grm="стр"] | [PART root grm="стр"] | [НАРЕЧ_ГЛАГОЛ root grm="стр"]) ([NOUN grm="тв"] | [ГЕНИТ_ИГ grm="тв"] | [ПРИЛ_СУЩ grm="тв"]);
