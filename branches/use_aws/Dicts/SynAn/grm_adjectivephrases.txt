
%% == format_for_modif_adj_groups ==
[ADJ_MOD] -> ('такой' | 'самый');
[МОДИФ_ПРИЛ1] -> [ADJ] | [SH-ADJ];
[МОДИФ_ПРИЛ] -> [ADJ_MOD] [МОДИФ_ПРИЛ1 root]
		    : $0.grm := agrcng($1.grm, $2.grm);

%% == format_for_adv_adj ==
%% слишком натренированы - ?
%% очень интересно - омонимы удаляются 
%% очень непросто - омонимы удаляются 

[ADV_ADJ] -> [TOKEN filename="adv_adj.txt"];
[НАР_ПРИЛ1] -> [ADJ] | [SH-ADJ] | [PART] | [SH-PART] | [АНАТ_СРАВН] | [ОДНОР_ПРИЛ] | [СРАВН_СТЕПЕНЬ];
[НАР_ПРИЛ] -> [ADV_ADJ] [НАР_ПРИЛ1 root];
[НАР_ПРИЛ] -> [ADV_ADJ] [PARTICLE] [НАР_ПРИЛ1 root];
[НАР_ПРИЛ] -> [UZH] [НАР_ПРИЛ];

%% == format_for_similar_adjs ==
[ОДНОР_ПРИЛ1] -> [ADJ] | [SH-ADJ] | [НАР_ПРИЛ] | [МОДИФ_ПРИЛ] | [АНАТ_СРАВН] | [СРАВН_СТЕПЕНЬ] | [PART] | [SH-PART] | [ПРЯМ_ДОП grm="ПРИЧАСТИЕ"];
[ОДНОР_ПРИЛ2] -> [ОДНОР_ПРИЛ1] [COMMA] [ОДНОР_ПРИЛ1 root]
		    : $0.grm := agrcng($1.grm, $3.grm);		    
[ОДНОР_ПРИЛ2] -> [ОДНОР_ПРИЛ1] [COMMA] [ОДНОР_ПРИЛ2 root]
		    : $0.grm := agrcng($1.grm, $3.grm);
[ОДНОР_ПРИЛ] -> ([ОДНОР_ПРИЛ1] | [ОДНОР_ПРИЛ2]) [CONJ] [ОДНОР_ПРИЛ1 root]
		    : $0.grm := agrcng($1.grm, $3.grm);    
[ОДНОР_ПРИЛ] -> [ОДНОР_ПРИЛ2];		    

%% красной и синей ракетками
%% == format_for_similar_comp_adjs ==
[ОДНОР_ПРИЛ3] -> [ADJ grm="сравн"] | [СРАВН_СТЕПЕНЬ];
[ОДНОР_ПРИЛ4] -> [ОДНОР_ПРИЛ1] [COMMA] [ОДНОР_ПРИЛ1 root];		    
[ОДНОР_ПРИЛ4] -> [ОДНОР_ПРИЛ3] [COMMA] [ОДНОР_ПРИЛ4 root];		    
[ОДНОР_ПРИЛ] -> ([ОДНОР_ПРИЛ3] | [ОДНОР_ПРИЛ4]) [CONJ] [ОДНОР_ПРИЛ3 root];		    
[ОДНОР_ПРИЛ] -> [ОДНОР_ПРИЛ4];

%% == format_for_comp_adv ==
[СРАВН_СТЕПЕНЬ] -> [TOKEN filename="comp_adv.dat"] [TOKEN grm="сравн" root];

%% == format_for_anat_compar ==
[COMP] -> [TOKEN form="более" grm="Н"] | [TOKEN form="менее" grm="Н"];
[АНАТ_СРАВН1] -> [ADJ] | [SH-ADJ] | [PART];
[АНАТ_СРАВН] -> [COMP] [АНАТ_СРАВН1 root];

[ROOT] -> [МОДИФ_ПРИЛ] | [НАР_ПРИЛ] | [АНАТ_СРАВН] | [ОДНОР_ПРИЛ] | [СРАВН_СТЕПЕНЬ];
